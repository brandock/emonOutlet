
//Brandon Baldock
//RF outlet monitor for measuring current. It sends the current in milliamps, as well as 1 for on (over a threshhold) and 0 for off. Then the supply voltage in millivolts.
//
//     int current;  // current in milliamps
//     int OnOrOff; //  1 for on, 0 for off
//     int supplyV;  // Supply voltage in millivolts
//
//Uses EmonLib from OpenEnergyMonitor: https://openenergymonitor.org
//Sketched based on TinyTx Send by Nathan Chantrell http://nathan.chantrell.net/tinytx
//Hardware based on Tinkerman emonShield: https://tinkerman.cat/post/moteino-energy-monitor-shield/
//emonShield is designed to work with Moteino, a versatile low power Arduino based on the Atmel ATMega328P microcontroller from LowPowerLab: https://lowpowerlab.com

//Note about hardware: This sketch is for a TinyTx current sensor with a 120 ohm burden, which lets it measure up to 19 amps.
//Note about the adaptation of the TinyTx code: It is very important to remove the TinyTx code that puts the analog to digital converter to sleep.
//That code works well for temperature measurement (which was a main use case of the original code), but emonlib needs it for current measurement.

//----------------------------------------------------------------------------------------------------------------------
// TinyTX Send test, sends an incrementing counter and battery voltage every 10 seconds
// By Nathan Chantrell. For hardware design see http://nathan.chantrell.net/tinytx
//
// Licenced under the Creative Commons Attribution-ShareAlike 3.0 Unported (CC BY-SA 3.0) licence:
// http://creativecommons.org/licenses/by-sa/3.0/
//
// Requires Arduino IDE with arduino-tiny core: http://code.google.com/p/arduino-tiny/
//----------------------------------------------------------------------------------------------------------------------

#define RF69_COMPAT 1
#include <JeeLib.h> // https://github.com/jcw/jeelib
#include "EmonLib.h"

ISR(WDT_vect) { Sleepy::watchdogEvent(); } // interrupt handler for JeeLabs Sleepy power saving

#define myNodeID 17     // RF12 node ID in the range 1-30
#define network 210      // RF12 Network group
#define freq RF12_433MHZ // Frequency of RFM12B module
#define Ical 16.67      // (2000 turns / 120 Ohm burden) = 16.67
#define inputPin 3        // for emonShield this will be 1, 2, or 3 based on which choice is made with the solder pads on the PCB
#define onThreshhold .1   // for checking the state of the measured appliance or device. If the amps used exceeds this threshhold the sketch will send OnOrOff=1

//#define USE_ACK           // Enable ACKs, comment out to disable
#define RETRY_PERIOD 5    // How soon to retry (in seconds) if ACK didn't come in
#define RETRY_LIMIT 5     // Maximum number of times to retry
#define ACK_TIME 10       // Number of milliseconds to wait for an ack


//########################################################################################################################
//Data Structure to be sent
//########################################################################################################################

 typedef struct {
      int current;  // current
      int OnOrOff; //  1 for on, 0 for off
      int supplyV;  // Supply voltage
 } Payload;

 Payload tinytx;
 
 EnergyMonitor emon1;

// Wait a few milliseconds for proper ACK
 #ifdef USE_ACK
  static byte waitForAck() {
   MilliTimer ackTimer;
   while (!ackTimer.poll(ACK_TIME)) {
     if (rf12_recvDone() && rf12_crc == 0 &&
        rf12_hdr == (RF12_HDR_DST | RF12_HDR_CTL | myNodeID))
        return 1;
     }
   return 0;
  }
 #endif

//--------------------------------------------------------------------------------------------------
// Send payload data via RF
//-------------------------------------------------------------------------------------------------
 static void rfwrite(){
  #ifdef USE_ACK
   for (byte i = 0; i <= RETRY_LIMIT; ++i) {  // tx and wait for ack up to RETRY_LIMIT times
     rf12_sleep(-1);              // Wake up RF module
      while (!rf12_canSend())
      rf12_recvDone();
      rf12_sendStart(RF12_HDR_ACK, &tinytx, sizeof tinytx); 
      rf12_sendWait(2);           // Wait for RF to finish sending while in standby mode
      byte acked = waitForAck();  // Wait for ACK
      rf12_sleep(0);              // Put RF module to sleep
      if (acked) { return; }      // Return if ACK received
  
   Sleepy::loseSomeTime(RETRY_PERIOD * 1000);     // If no ack received wait and try again
   }
  #else
     rf12_sleep(-1);              // Wake up RF module
     rf12_sendNow(0, &tinytx, sizeof tinytx); 
     rf12_sendWait(2);           // Wait for RF to finish sending while in standby mode
     rf12_sleep(0);              // Put RF module to sleep
     return;
  #endif
 }


//--------------------------------------------------------------------------------------------------
// Read supply voltage
//--------------------------------------------------------------------------------------------------
 long readVcc() {
   //bitClear(PRR, PRADC); ADCSRA |= bit(ADEN); // Enable the ADC -- from Nathan Chantrall's sketch. This won't work with current monitoring.
   long result;
   // Read 1.1V reference against Vcc
   #if defined(__AVR_ATtiny84__) 
    ADMUX = _BV(MUX5) | _BV(MUX0); // For ATtiny84
   #else
    ADMUX = _BV(REFS0) | _BV(MUX3) | _BV(MUX2) | _BV(MUX1);  // For ATmega328
   #endif 
   delay(2); // Wait for Vref to settle
   ADCSRA |= _BV(ADSC); // Convert
   while (bit_is_set(ADCSRA,ADSC));
   result = ADCL;
   result |= ADCH<<8;
   result = 1126400L / result; // Back-calculate Vcc in mV
   //ADCSRA &= ~ bit(ADEN); bitSet(PRR, PRADC); // Disable the ADC to save power -- from Nathan Chantrall's sketch. This won't work with current monitoring.
   return result;
} 


//########################################################################################################################

void setup() {

  // initialize serial communication:
  Serial.begin(38400);
  Serial.println("Started...");
  Serial.flush();

  // Measure current. First parameter is the analalog input, 1 for A1, 2 for A2, or 3 for A3.
  // The second parameter is the calibration factor, i.e. CT Ratio / Burden resistance = (100A / 0.05A) / 120 Ohms = 16.67 (same as for the emonTx V3 CT4)
  emon1.current(inputPin, Ical); 
  
  rf12_initialize(myNodeID,freq,network); // Initialize RFM12 with settings defined above 
  rf12_sleep(0);                          // Put the RFM12 to sleep

  PRR = bit(PRTIM1); // only keep timer 0 going
  
  //ADCSRA &= ~ bit(ADEN); bitSet(PRR, PRADC); // Disable the ADC to save power -- from Nathan Chantrall's sketch. This won't work with current monitoring.

  //delay(30000);  // it takes about 30 seconds for the current transformer to settle down after powering up -- so it might make sense to delay ouput, but then one wonders if the monitor is working
}

void loop() {
    
  tinytx.supplyV = readVcc(); // Get supply voltage
  Serial.print(tinytx.supplyV); Serial.println(" volts supply*100");
  Serial.flush();

  double Irms = emon1.calcIrms(1480);  // Calculate Irms using EmonLib
  tinytx.current = Irms*100;
  Serial.print(tinytx.current); Serial.println(" amps");
  Serial.flush();

  if (tinytx.current/100 > onThreshhold) tinytx.OnOrOff = 1;  //if use is over onThreshhold amps, the measured appliance is on
  else tinytx.OnOrOff = 0;
  
  rfwrite(); // Send data via RF 



  Sleepy::loseSomeTime(10000); //JeeLabs power save function: enter low power mode for 10 seconds (valid range 16-65000 ms)
    
}
